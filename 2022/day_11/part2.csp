import "std.csp";
import "libc/stdio.csp";

macro ROUNDS { 10000 }

type Monkey: struct {
    items: &i64,
    operation: &char,
    test: i32,
    on_true: i32,
    on_false: i32,
    inspections: i32
};

namespace monkey {
    fn operation(op: &char, old: i64): i64 {
        using std::c_str;

        let operands: char'c[10]'c[2];
        let operator = '\0';

        libc::sscanf(op, "%s %c %s", &operands[0], &operator, &operands[1]);
       
        let a = operands[0] |> if $[0] == 'o' => old else to_i64($);
        let b = operands[1] |> if $[0] == 'o' => old else to_i64($);

        match operator {
            '+' => ret a + b;
            '*' => ret a * b;
            _ => assert!(false, "unknown operator");
        }

        <- 0;
    }

    fn inspect(monkey: &Monkey, index: i32, modulo: u64) {
        monkey.items[index] = operation(monkey.operation, monkey.items[index]) % modulo;
        monkey.inspections++;
    }

    fn round(monkeys: Monkey[], num_monkeys: i32, modulo: u64) {
        for let i = 0; i < num_monkeys; i++; {
            for let j = vec_size!(monkeys[i].items) - 1; j >= 0; j--; {
                inspect(&monkeys[i], j, modulo);
                vec_pop!(monkeys[i].items) |> vec_push!(monkeys[if $ % monkeys[i].test == 0 => monkeys[i].on_true else monkeys[i].on_false].items, $);
            }
        }
    }
}

fn main(): i32 {
    using std;

    with f = file!("input.txt") {
        let input = file::read_all(f);
        let lines = string::split(input, "\n");
        
        let num_monkeys = 0;
        let monkeys: Monkey[10];
        let modulo: u64 = 1;

        for let i = 0; i < vec_size!(lines); i += 7; {
            let index = 0;
            assert!(libc::sscanf(lines[i], "Monkey %d:", &index) == 1, lines[i]);

            if index + 1 > num_monkeys
                num_monkeys = index + 1;
            assert!(num_monkeys <= len monkeys);

            let items = string::split(&lines[i + 1][len "  Starting items:"], ",");
            monkeys[index].items = vec![i64];
            for let j = 0; j < vec_size!(items); j++;
                vec_push!(monkeys[index].items, c_str::to_i64(items[j]));
            vec_free!(items);

            monkeys[index].operation = &lines[i + 2][len "  Operation: new = "];
            assert!(libc::sscanf(lines[i + 3], "  Test: divisible by %d", &monkeys[index].test) == 1);
            assert!(libc::sscanf(lines[i + 4], "    If true: throw to monkey %d", &monkeys[index].on_true) == 1);
            assert!(libc::sscanf(lines[i + 5], "    If false: throw to monkey %d", &monkeys[index].on_false) == 1);

            modulo *= monkeys[index].test;
        }

        for let i = 0; i < ROUNDS!; i++; {
            monkey::round(monkeys, num_monkeys, modulo);
        }

        let business = [0: u64, 0];
        for let i = 0; i < num_monkeys; i++; {
            let inspections = monkeys[i].inspections;
            if inspections > business[0] {
                business[1] = business[0];
                business[0] = inspections;
                continue;
            }
            
            else if inspections > business[1] {
                business[1] = inspections;
            }
            vec_free!(monkeys[i].items);
        }

        io::printf("monkey business level: %i * %i => %l\n", ...business, business[0] * business[1]);

        vec_free!(lines);
        free!(input);
    }
    <- 0;
}
